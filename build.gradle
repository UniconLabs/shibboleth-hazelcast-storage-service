plugins {
    id 'java'
    id 'idea'
    id 'distribution'
    id 'com.jfrog.bintray' version '1.6'
    id 'net.researchgate.release' version '2.4.0'
}

apply from: 'gradle/shibboleth.gradle'

repositories {
    maven { url 'https://build.shibboleth.net/nexus/content/groups/public' }
    jcenter()
}

dependencies {
    compileOnly "org.opensaml:opensaml-storage-api:${project.'opensaml.version'}"
    compileOnly "ch.qos.logback:logback-core:${project.'logback.version'}"

    compile "com.hazelcast:hazelcast-all:${project.'hazelcast.version'}"
    testCompile "net.shibboleth.idp:idp-authn-api:${project.'shibboleth.version'}"
    testCompile "org.opensaml:opensaml-storage-api:${project.'opensaml.version'}:tests@jar"
    testCompile "junit:junit:4.12"
    testCompile "org.testng:testng:6.9.9"
}

distributions {
    main {
        contents {
            duplicatesStrategy = 'exclude'
            into ('edit-webapp/WEB-INF/lib') {
                from { project.jar }
                from { project.configurations.compile }
            }
        }
    }
}

task uploadDist(type: com.jfrog.bintray.gradle.BintrayUploadTask) {
    apiUrl = 'https://api.bintray.com'
    user = bintrayUsername
    apiKey = bintrayAPIKey
    userOrg = 'uniconiam'
    packageName = rootProject.name
    repoName = 'generic'

    versionName = project.version
    packageLicenses = ['Apache-2.0']
    packageVcsUrl = 'https://github.com/UniconLabs/shibboleth-hazelcast-storage-service.git'

    dependsOn project.task(type: com.jfrog.bintray.gradle.RecordingCopyTask, com.jfrog.bintray.gradle.RecordingCopyTask.NAME).with {
        ConfigureUtil.configure({
            from distZip
            from distTar
            into '.'
        }, it)
        it.outputs.upToDateWhen {false}
        return it
    }
}

afterReleaseBuild.dependsOn uploadDist

task wrapper(type: Wrapper) {
    gradleVersion = 3.5
}

idea {
    module {
        scopes.PROVIDED.plus += [configurations.compileOnly]
    }
}
