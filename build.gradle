plugins {
    id 'java'
    id 'idea'
    id 'distribution'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'net.researchgate.release' version '2.7.0'
}

apply from: 'gradle/shibboleth.gradle'

repositories {
    maven { url 'https://build.shibboleth.net/nexus/content/groups/public' }
    jcenter()
}

dependencies {
    compileOnly "org.opensaml:opensaml-storage-api:${project.'opensaml.version'}"
    compileOnly "ch.qos.logback:logback-core:${project.'logback.version'}"

    compile "com.hazelcast:hazelcast-all:${project.'hazelcast.version'}"
    testCompile "net.shibboleth.idp:idp-authn-api:${project.'shibboleth.version'}"
    testCompile "org.opensaml:opensaml-storage-api:${project.'opensaml.version'}:tests@jar"
    testCompile "junit:junit:4.12"
    testCompile "org.testng:testng:6.9.9"
}

test {
    useTestNG()
    scanForTestClasses = false
    include 'net/unicon/iam/shibboleth/**/*'
}

distributions {
    main {
        contents {
            duplicatesStrategy = 'exclude'
            into ('edit-webapp/WEB-INF/lib') {
                from { project.jar }
                from { project.configurations.compile }
            }
        }
    }
}

bintray {
    user = bintrayUsername
    key = bintrayAPIKey
    pkg {
        repo = 'generic'
        name = rootProject.name
        userOrg = 'uniconiam'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/UniconLabs/shibboleth-hazelcast-storage-service.git'
        version {
            name = project.version
        }
        filesSpec {
            tasks.getByName('distZip')
            tasks.getByName('distTar')
            into '.'
        }
    }
}

afterReleaseBuild.dependsOn bintrayUpload

wrapper {
    gradleVersion = '4.10.2'
}

idea {
    module {
        scopes.PROVIDED.plus += [configurations.compileOnly]
    }
}
